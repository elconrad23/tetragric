// This file is part of midnightntwrk/example-counter.
// Copyright (C) 2025 Midnight Foundation
// SPDX-License-Identifier: Apache-2.0
// Licensed under the Apache License, Version 2.0 (the "License");
// You may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Deployed contract example: 02008340669a88d88c9897d92d4661692710c4fe01ef038d0fdc97f1c3027df47c89

pragma language_version 0.16;

import CompactStandardLibrary;

export enum STATE { vacant, occupied }

export ledger state: STATE;
export ledger message: Maybe<Opaque<"string">>;
export ledger instance: Counter;
export ledger poster: Bytes<32>;

constructor() {
    state = STATE.vacant;
    message = none<Opaque<"string">>();
    instance.increment(1);
}

witness local_secret_key(): Bytes<32>;

export circuit post(new_message: Opaque<"string">): [] {
    assert (state == STATE.vacant, "Attempted to post to an occupied board");
    poster = disclose(public_key(local_secret_key(), instance as Field as Bytes<32>));
    message = disclose(some<Opaque<"string">>(new_message));
    state = STATE.occupied;
}

export circuit take_down(): Opaque<"string"> {
    assert (state == STATE.occupied, "Attempted to take down post from an empty board");
    assert (poster == public_key(local_secret_key(), instance as Field as Bytes<32>), "Attempted to take down post, but not the current poster");
    const former_msg = message.value;
    state = STATE.vacant;
    instance.increment(1);
    message = none<Opaque<"string">>();
    return former_msg;
}

export circuit public_key(sk: Bytes<32>, instance: Bytes<32>): Bytes<32> {
    return persistentHash<Vector<3, Bytes<32>>>([pad(32, "bboard:pk:"), instance, sk]);
}
